# Copyright 2015 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

# ==============================================================================
# TEST SETUP
# ==============================================================================

# Define a test as an executable (or apk on Android) with the "testonly" flag
# set.
template("test") {
  assert(!is_android)
  executable(target_name) {
    deps = []
    forward_variables_from(invoker, "*")

    testonly = true
    deps += [
      # All shared libraries must have the sanitizer deps to properly link in
      # asan mode (this target will be empty in other cases).
      "//build/config/sanitizers:deps",

      # Give tests the default manifest on Windows (a no-op elsewhere).
      "//build/win:default_exe_manifest",
    ]
  }
}
