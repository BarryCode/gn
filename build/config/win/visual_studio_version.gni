# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # Version of Visual Studio pointed to by the visual_studio_path.
  # Use "2015" for Visual Studio 2015.
  visual_studio_version = "2015"

  # Path to Visual Studio.
  visual_studio_path = ""

  # Full path to the Windows SDK, not including a backslash at the end.
  # This value is the default location, override if you have a different
  # installation location.
  windows_sdk_path = ""

  # Path of the Windows Clang installation
  win_clang_prefix = ""
}

assert(visual_studio_version != "",
       "You must set visual_studio_version on Windows")

if (visual_studio_path == "") {
  visual_studio_path = exec_script("toolchain.py",
                                   [
                                     "get_vs_dir",
                                     visual_studio_version,
                                   ],
                                   "trim string")
}

if (is_clang) {
  assert(win_clang_prefix != "",
         "You must set win_clang_prefix when using Clang on Windows")
}

if (is_clang) {
  sys_include_prefix = "-imsvc"
} else {
  # MSVC doesn't have the concept of system headers.
  sys_include_prefix = "/I"
}

if (host_os == "win") {
  clang_cl = "clang-cl.exe"
} else {
  clang_cl = "clang-cl"
}

toolchain_data = exec_script("toolchain.py",
                             [
                               "setup_toolchain",
                               visual_studio_path,
                               sys_include_prefix,
                             ],
                             "scope")

if (windows_sdk_path == "") {
  windows_sdk_path = toolchain_data.windows_sdk_path
}

# These are hardcoded in toolchain.py
x86_environment_path = "environment.x86"
x64_environment_path = "environment.x64"
# TODO(tim): WinRT
